apply plugin: 'com.android.application'
apply plugin: 'com.mikepenz.aboutlibraries.plugin'

android {
    compileSdkVersion 32
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
//    kotlinOptions {
//        jvmTarget = "11"
//    }

    packagingOptions {
        // From the JGit jars
        exclude 'plugin.properties'
    }

    defaultConfig {
        applicationId "se.splushii.dancingbunnies"
        minSdkVersion 26
        targetSdkVersion 32
        versionCode 9
        versionName "0.1.3"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    signingConfigs {
        debug {
            keyAlias "debugKey"
            keyPassword "debugpassword"
            storeFile file("../debug.jks")
            storePassword "debugpassword"
        }
        release {
            keyAlias "releaseKey"
            keyPassword System.getenv("KEYPWD")
            storeFile file(System.getenv("KEYSTOREPATH") ?: "release.jks")
            storePassword System.getenv("KEYSTOREPWD")
        }
    }
    buildTypes {
        debug {
            resValue "string", "app_name", "debug Bunnies"
            resValue "string", "app_name_logo", "db"
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            resValue "string", "app_name", "dancing Bunnies"
            resValue "string", "app_name_logo", "dB"
            signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails depDetails ->
        if (depDetails.requested.group == "com.jcraft" && depDetails.requested.name == "jsch") {
            depDetails.useTarget("com.github.mwiede:jsch:0.2.2")
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.5.1"
    implementation "androidx.room:room-runtime:2.4.3"
    annotationProcessor "androidx.room:room-compiler:2.4.3"
    implementation "androidx.work:work-runtime:2.7.1"
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.mediarouter:mediarouter:1.3.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'androidx.preference:preference:1.2.0'

    implementation 'com.google.guava:guava:30.0-android'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'com.google.android.gms:play-services-cast:21.0.1'
    implementation 'com.google.android.gms:play-services-cast-framework:21.0.1'

    implementation "com.mikepenz:aboutlibraries-core:8.5.0"
    implementation "com.mikepenz:aboutlibraries:8.5.0"

    implementation "com.networknt:json-schema-validator:1.0.40"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.11.0"

    implementation "org.eclipse.jgit:org.eclipse.jgit:6.2.0.202206071550-r"
    implementation "org.eclipse.jgit:org.eclipse.jgit.ssh.jsch:6.2.0.202206071550-r"

    implementation 'com.github.mwiede:jsch:0.2.2'

    implementation "org.bouncycastle:bcpkix-jdk15on:1.70"

    implementation 'commons-io:commons-io:2.7'

    testImplementation 'junit:junit:4.13.2'

    // Android is missing ClassValue. Needed to update Lucene.
    implementation 'org.apache.lucene:lucene-core:4.8.1'
    implementation 'org.apache.lucene:lucene-queryparser:4.8.1'
    implementation 'org.apache.lucene:lucene-analyzers-common:4.8.1'
}
